<!DOCTYPE html>
<!-- saved from url=(0179)http://www.uqac.ca/daudet/Cours/Infographie/DOCUMENTS/repertoire435/DOCUMENTS-MULTIMEDIA-COMPLETS/Interactive-Computer-Graphics-7ed/SOURCES/Source%20Code/Class/spherecylinder.html -->
<html><head><meta http-equiv="Content-Type" content="text/html; charset=UTF-8">

<title>WebGL - Projet de conception</title>

    <script type="x-shader/x-vertex" id="vshaderbox">
        uniform mat4 projection;
        uniform mat4 modelview;

        attribute vec3 vcoords;
        attribute vec3 vnormal;
        attribute vec2 vtexcoord;

        varying vec3 fCoords;

        void main() {
            vec2 tmp = vtexcoord;
            vec3 tmp2 = vnormal;
            // inserted only to map texcoord and normal in this shader
            // (required since models uses texture coordinates even though
            //   we will not use them in this shader)

            vec4 eyeCoords = modelview * vec4(vcoords,1.0);
            gl_Position = projection * eyeCoords;
            fCoords = vcoords;  // les sommets du cube positionnés à -500 ou +500,
                                // on devrait ici diviser par 500 mais textureCube() s'en accommode !!!
        }
    </script>
    <script type="x-shader/x-fragment" id="fshaderbox">
         precision mediump float;

         varying vec3 fCoords;

         uniform samplerCube envbox;

         void main() {
              gl_FragColor = textureCube(envbox, fCoords);
         }
    </script>


<script type="x-shader/x-vertex" id="vshader">
     uniform mat4 projection;
     uniform mat4 modelview;
     uniform vec4 lightPosition;
     uniform mat3 normalMatrix;

     attribute vec3 vcoords;
     attribute vec3 vnormal;
     attribute vec2 vtexcoord;

     varying vec3 fN, fL, fE;
     varying vec3 fviewCoords;
     varying vec2 fTexCoord;

     void main() {
        vec4 eyeCoords = modelview * vec4(vcoords,1.0);
        gl_Position = projection * eyeCoords;

        vec3 pos = eyeCoords.xyz;

          // Modify light position according to the
          // transformations applied if you want the light
          // to move with the objects
//        vec3 light = (modelview * lightPosition).xyz;
        vec3 light = (lightPosition).xyz;

          // Now compute vectors L, E and N.
          // Note that N is affected by the
          // transformations applied.

        fL = normalize( light - pos );
        fE = normalize(-pos);
        fN = normalMatrix * normalize(vnormal);

        fviewCoords = eyeCoords.xyz;
        fTexCoord = vtexcoord;
     }
</script>

<script type="x-shader/x-fragment" id="fshader">
     precision mediump float;

     uniform vec4 ambientProduct;
     uniform vec4 diffuseProduct;
     uniform vec4 specularProduct;
     uniform float shininess;

     varying vec3 fN, fL, fE;
     varying vec3 fviewCoords;
     varying vec2 fTexCoord;

     uniform sampler2D texture;
     uniform bool hasTexture;

     uniform float alpha;

     varying vec3 fNormal;

     void main() {

          vec4 Color;

          vec3 H = normalize( fL + fE );
          vec4 ambient = ambientProduct;

          float factorLN = max( dot(fL, fN), 0.0 );
          vec4  diffuse = factorLN*diffuseProduct;

          float factorNH = pow( max(dot(fN, H), 0.0), shininess );
          vec4  specular = factorNH * specularProduct;

          if( dot(fL, fN) < 0.0 )
              specular = vec4(0.0, 0.0, 0.0, 1.0);

          Color = ambient + diffuse + specular;
          Color.a = alpha;

          if(hasTexture) {
              gl_FragColor = Color * texture2D( texture, fTexCoord );
          }
          else {
              gl_FragColor = Color;
          }


     }
</script>

    <script type="x-shader/x-vertex" id="vshadermap">
     uniform mat4 projection;
     uniform mat4 modelview;

     attribute vec3 vcoords;
     attribute vec3 vnormal;
     attribute vec2 vtexcoord;

     varying vec3 fviewCoords;
     varying vec3 fNormal;

     void main() {
        vec2 tmp = vtexcoord;
       // inserted only to map texcoord in this shader (required
       // since models uses texture coordinates even though we will
       // not use them in environmental mapping)

        vec4 eyeCoords = modelview * vec4(vcoords,1.0);
        gl_Position = projection * eyeCoords;
        fviewCoords = eyeCoords.xyz;
        fNormal = vnormal;
     }
</script>

    <script type="x-shader/x-fragment" id="fshadermap">
     precision mediump float;

     varying vec3 fNormal;
     varying vec3 fviewCoords;

     uniform samplerCube skybox;
     uniform mat3 normalMatrix;
     uniform mat3 minv;

     // color map
     uniform vec4 ambientProduct;
     uniform float alpha;

     void main() {
          vec3 N = normalMatrix * fNormal;
          vec3 V = -fviewCoords;
          vec3 R = -reflect(V,N);
                             // note the reflect() function
                             // computes the equation below but
                             // inverts the result (!!!)

          // Transform by inverse of view transform, which was applied to the skybox
          R = minv * R;

          gl_FragColor = textureCube(skybox, R);
     }
</script>

    <script type="text/javascript" src="Common/simple-rotator.js"></script>
    <script type="text/javascript" src="Common/MV.js"></script>
    <script type="text/javascript" src="Common/basic-objects-IFS.js"></script>
    <script type="text/javascript" src="Common/webgl-utils.js"></script>
    <!-- custom -->
    <script type="text/javascript" src="Common/custom-MV.js"></script>
    <script type="text/javascript" src="Common/custom-basic-object.js"></script>
    <!-- /custom -->

    <!-- project -->
    <script type="text/javascript" src="js/texture.js"></script>
    <script type="text/javascript" src="js/models.js"></script>
    <script type="text/javascript" src="js/phong-color.js"></script>
    <script type="text/javascript" src="js/right-child-left-sibling.js"></script>
    <script type="text/javascript" src="js/spacecraft.js"></script>
    <script type="text/javascript" src="js/planets.js"></script>
    <!-- /project -->
    <script type="text/javascript" src="js/jedi-starfighter.js"></script>

</head>
<body style="background-color:#DDD">

<h1>Projet infographie: Delta-7 Aethersprite-class light interceptor</h1>
<pre>The model is in this link: <a href="https://vignette.wikia.nocookie.net/starwars/images/7/79/Jedi_Starfighter_EpII.png">https://vignette.wikia.nocookie.net/starwars/images/7/79/Jedi_Starfighter_EpII.png</a></pre>
<h3>Author: NGUYEN Duc Hau</h3>
<h2>Instructions: </h2>
<p>Move the camera forward/backward (<strong>arrow up/down</strong>), horizontally left/right (<strong>arrow left/right</strong>) and verticaly up/down (<strong>Ctrl + arrow up/down</strong>)</p>
<p>Click and drag the mouse to change the camera's vision angle</p>
<noscript>&lt;hr&gt;&lt;h3&gt;This page requires Javascript and a web browser that supports WebGL&lt;/h3&gt;&lt;hr&gt;</noscript>


<div>

    <canvas width="600" height="600" id="glcanvas" style="background-color:blue"></canvas>

</div>



</body></html>
